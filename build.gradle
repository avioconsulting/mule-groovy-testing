plugins {
    id 'com.intershop.gradle.jaxb' version '2.0.0'
}

group 'com.avioconsulting.mule'
version '1.0.0'

configurations {
    provided
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven'

sourceSets {
    main {
        compileClasspath += configurations.provided
        java {
            exclude 'com/quintiles/schemas/soaptest/v1/**'
        }
    }
    test {
        java {
            srcDirs "${project.buildDir}/generated/jaxb/java/soaptest"
        }
    }
}

sourceCompatibility = 1.5

def getMavenCreds = { String id ->
    def m2Dir = new File(System.getProperty('user.home'), '.m2')
    def settingsXmlPath = new File(m2Dir, 'settings.xml')
    assert settingsXmlPath.exists()
    def xmlNode = new XmlParser().parse(settingsXmlPath.absolutePath)
    def serverCreds = xmlNode.servers.server.find { node -> node.id.text() == id }
    assert serverCreds != null
    [user: serverCreds.username.text(), password: serverCreds.password.text()]
}

def eeCreds = getMavenCreds('avio-mule-ee-releases')

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "http://repository.mulesoft.org/releases"
    }
    maven {
        url "http://repo.spring.io/plugins-release/"
    }
    maven {
        // staxutils
        url "https://repository-master.mulesoft.org/nexus/content/groups/public/"
    }
    maven {
        credentials {
            username eeCreds.user
            password eeCreds.password
        }
        url "https://devops.avioconsulting.com/nexus/repository/mulesoft-ee-releases/"
    }
}

jaxb {
    javaGen {
        soaptest {
            schema = file('src/test/resources/soap/SOAPTest_v1.xsd')
        }

        xmlroot {
            schema = file('src/test/resources/soap_xmlroot/SOAPTest_v1.xsd')
            packageName = 'com.avioconsulting.mule.testing.soapxmlroot'
        }
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: 'https://devops.avioconsulting.com/nexus/repository/avio-releases/') {
                authentication userName: eeCreds.user,
                               password: eeCreds.password
            }
        }
    }
}

dependencies {
    def muleVersion = '3.8.5'
    def munitVersion = '1.2.1'
    def munitSupportVersion = '3.8.2'
    compile 'org.apache.maven:maven-plugin-api:3.3.9'
    compile group: 'com.mulesoft.muleesb', name: 'mule-core-ee', version: muleVersion
    compile group: 'org.mule.tests', name: 'mule-tests-functional', version: muleVersion
    compile group: 'org.mule.modules', name: 'mule-module-client', version: muleVersion
    compile group: 'org.mule.modules', name: 'mule-module-json', version: muleVersion
    compile group: 'com.mulesoft.munit', name: 'mule-munit-support', version: munitSupportVersion
    compile 'org.mule.modules:mule-module-sfdc:8.3.0'
    compile 'org.apache.cxf:cxf-api:2.7.18'
    compile 'xalan:xalan:2.7.2'
    // assumes testing being done with dataweave
    compile group: 'com.mulesoft.weave', name: 'mule-plugin-weave', version: muleVersion
    compile group: 'com.mulesoft.munit', name: 'munit-runner', version: munitVersion
    compile 'org.codehaus.groovy:groovy-all:2.4.6'
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testRuntime group: 'org.mule.transports', name: 'mule-transport-http', version: muleVersion
    testRuntime group: 'org.mule.transports', name: 'mule-transport-vm', version: muleVersion
    testRuntime group: 'org.mule.modules', name: 'mule-module-ws', version: muleVersion
    testRuntime group: 'org.mule.modules', name: 'mule-module-apikit', version: muleVersion
}
